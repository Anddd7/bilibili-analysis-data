apply plugin: 'io.spring.dependency-management'

// 依赖管理
dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-starter-parent:${SpringBootVersion}"
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${SpringCloudVersion}"
    }
    dependencies {
        ["com.fasterxml.jackson.core"    : ["jackson-databind", "jackson-core"],
         "com.fasterxml.jackson.datatype": ["jackson-datatype-jdk8", "jackson-datatype-jsr310"],
         "com.fasterxml.jackson.module"  : ["jackson-module-parameter-names"]
        ].forEach { groupName, artifacts ->
            dependencySet(group: groupName, version: "${JacksonVersion}") {
                artifacts.forEach { artifact -> entry artifact }
            }
        }
    }
}

// 依赖
dependencies {
    compile 'org.springframework.cloud:spring-cloud-starter-config'
    compile('org.springframework.boot:spring-boot-starter-web')
    testCompile('org.springframework.boot:spring-boot-starter-test')

    compile("io.springfox:springfox-swagger2:${SwaggerVersion}")
    compile("io.springfox:springfox-swagger-ui:${SwaggerVersion}")
}